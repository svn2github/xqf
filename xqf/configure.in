#
# Autoconf support for XQF
# Initially written by Markus Fischer <mfischer@josefine.ben.tuwien.ac.at>
#

AC_INIT(src/xqf.c)

AM_INIT_AUTOMAKE(xqf, 0.9.7a)
dnl AM_CONFIG_HEADER(src/gnuconfig.h:src/gnuconfig.h.in)
AM_CONFIG_HEADER(src/gnuconfig.h)

ALL_LINGUAS="de es"

dnl Checks for programs.
AC_PROG_CC
AC_ISC_POSIX

dnl Checks for header files.
AC_HEADER_STDC

AM_SANITY_CHECK

dnl Pick up the gettext, etc macros
dnl AM_ACLOCAL_INCLUDE(macros)
dnl AM_GNU_GETTEXT

dnl check for qstat
AC_ARG_WITH(qstat,[  --with-qstat=CMD        use CMD to run qstat])

AC_MSG_CHECKING(qstat version)

if test "x$with_qstat" != "x" ; then
	QSTATEXEC="$with_qstat"
	qstat_version=`$QSTATEXEC 2>/dev/null | grep version | cut -d' ' -f 3`
else
	QSTATEXEC="qstat"
	qstat_version=`$QSTATEXEC 2>/dev/null | grep version | cut -d' ' -f 3`
	if test "x$qstat_version" = "x" ; then
		QSTATEXEC="quakestat"
		qstat_version=`$QSTATEXEC 2>/dev/null | grep version | cut -d' ' -f 3`
	fi
fi

if test "x$qstat_version" = "x" ; then
	AC_MSG_RESULT(qstat not found)
	echo
	echo "*** QStat is *required* to run XQF"
	echo "*** Get it from http://qstat.org/"
	echo "*** and put it in your path"
	echo
	exit 1
else
	AC_MSG_RESULT($qstat_version)
fi

AC_SUBST(QSTATEXEC)

case "$qstat_version" in
	2.0*|2.1*|2.3*|2.4a|2.4b)
	echo
	echo "*** You have obsolete QStat beta version installed"
	echo "*** QStat 2.4c is *required* to run XQF"
	echo "*** Get it from http://qstat.org/"
	echo
	exit 1
	;;

	2.3c|2.3d|2.3e|2.3f)
	QSTAT23="-DQSTAT23"
	;;

	2.3*|2.4*)
	QSTAT23="-DQSTAT23 -DQSTAT_HAS_UNREAL_SUPPORT"
	;;
esac

AC_SUBST(QSTAT23)

AM_PROG_LIBTOOL

dnl check if user wants bzip2 compression instead of gzip
COMPRESSION="-DCOMPRESSOR_GZIP"
AC_ARG_ENABLE(bzip2,[  --enable-bzip2          use bzip2 for data compression],COMPRESSION="-DCOMPRESSOR_BZIP2")
AC_SUBST(COMPRESSION)

dnl check if user wants debug
AC_ARG_ENABLE(debug,[  --enable-debug          turn on debugging ],DEBUG="-g -DDEBUG", DEBUG="")
AC_SUBST(DEBUG)

AC_SUBST(VERSION)
AC_SUBST(QSTATEXEC)

dnl Pick up the gettext, etc macros
dnl AM_ACLOCAL_INCLUDE(macros)
AM_GNU_GETTEXT

AM_PATH_GTK(1.2.0,,exit 1)

AC_DEFINE_UNQUOTED(XQF_VERSION, "$VERSION", XQF Version number)
AC_DEFINE_UNQUOTED(QSTAT_EXEC, "$QSTATEXEC", QSTAT executable path)

# workaround for intl/ which requires config.h
echo "creating link config.h -> src/gnuconfig.h"
test -e config.h -a ! -L config.h && rm config.h
test ! -e config.h && ln -s src/gnuconfig.h config.h

rm -f intl/libintl.h
# damn gettext should do that itself!
if test "$USE_INCLUDED_LIBINTL" = "yes"; then
	echo "creating link intl/libintl.h -> libgnuintl.h"
	ln -s libgnuintl.h intl/libintl.h
fi

AC_OUTPUT([Makefile intl/Makefile po/Makefile.in \
src/Makefile src/xpm/Makefile \
docs/Makefile \
debian/Makefile])
